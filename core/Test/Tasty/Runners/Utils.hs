-- | Note: this module is re-exported as a whole from "Test.Tasty.Runners"
module Test.Tasty.Runners.Utils where

import Control.Concurrent.Async
import Control.Concurrent.Timeout
import Control.Exception
import Control.DeepSeq
import Control.Applicative
import Text.Printf

-- | Catch possible exceptions that may arise when evaluating a string and adds
-- a timeout to the evaluation. For normal (total) strings, this is a no-op.
--
-- This function should be used to display messages generated by the test
-- suite (such as test result descriptions).
--
-- See e.g. <https://github.com/feuerbach/tasty/issues/25>
--          <https://github.com/feuerbach/tasty/issues/89>
formatMessage :: String -> IO String
formatMessage msg = withTimeout $ go 3 msg
  where
    -- to avoid infinite recursion, we introduce the recursion limit
    go :: Int -> String -> IO String
    go 0        _ = return "exceptions keep throwing other exceptions!"
    go recLimit msg = do
      mbStr <- try $ evaluate $ force msg
      case mbStr of
        Right str -> return str
        Left e' -> printf "message threw an exception: %s" <$> go (recLimit-1) (show (e' :: SomeException))

    withTimeout act = do
      res <- withAsync act $ timeout 1000000 . wait
      return $ case res of
        Just msg -> msg
        Nothing -> "formatting the message ran into a timeout"
